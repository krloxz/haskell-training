Functoriality
=============

Functoriality refers to the fact that functors can be combined and composed to create larger structures.

Bifunctors
----------
Functors of two arguments.

A bifunctor is a functor mapping the cartesian product of two categories (including their objects and morphisms) into another category: `C x D -> E`

![Bifunctor](https://user-images.githubusercontent.com/7043153/128787140-5fe2da2a-b292-4229-a4da-b961d40229bb.png)

Haskell definition:
```haskell
class Bifunctor f where
    bimap :: (a -> c) -> (b -> d) -> f a b -> f c d
    bimap g h = first g . second h
    first :: (a -> c) -> f a b -> f c b
    first g = bimap g id
    second :: (b -> d) -> f a b -> f a d
    second = bimap id
```

Functoriality of Type Constructors
----------------------------------

### Product & Sum
Product and sum combine two types to create new ones, so they are bifunctos that apply the first function to the first type and the second function to the second type:
```haskell
instance Bifunctor (,) where
    bimap f g (x, y) = (f x, g y)

instance Bifunctor Either where
    bimap f _ ( Left x) = Left (f x)
    bimap _ g ( Right y) = Right (g y)
```

### Function
The `function` constructor, similarly to product and sum, also takes two types to produce a new one; but with a big difference: functions aren't covariant in both arguments, they are contravariant in the first one. For this reason the functor for the second type argument of `function` and its bifunctor need special classes:

```haskell
-- Functor for contravariant arguments
class Contravariant f where
    contramap :: (b -> a) -> (f a -> f b)

-- Functor on the first function argument
type Op r a = a -> r
instance Contravariant ( Op r) where
    -- (b -> a) -> Op r a -> Op r b
    -- (b -> a) -> (a -> r) -> (b -> r)
    contramap f g = g . f

-- Bifunctor for types that are contravariant in the first argument
class Profunctor p where
    dimap :: (a -> b) -> (c -> d) -> p b c -> p a d
    dimap f g = lmap f . rmap g
    lmap :: (a -> b) -> p b c -> p a c
    lmap f = dimap f id
    rmap :: (b -> c) -> p a b -> p a c
    rmap = dimap id

-- Bifunctor for functions
instance Profunctor ( -> ) where
    -- (a -> b) -> (c -> d) -> (b -> c) -> (a -> d)
    dimap f g h = g . h . f
    -- (a -> b) -> (b -> c) -> (a -> c)
    lmap f g = g . f
    -- (b -> c) -> (a -> b) -> (a -> c)
    rmap f g = f . g
```

Functorial Algebraic Data Types
-------------------------------
Given that product and sum types are functorial and considering that basic building blocks like `Const` and `Identity` are also functors:

```haskell
-- Can be used to define empty containers
instance Bifunctor Const where
    bimap f _ (Const a) = Const (f a)

-- Can be used to define single-element containers
instance Functor Identity where
    fmap f ( Identity x) = Identity (f x)
```

All ADT's can be defined (up to isomorphism) using a composition of functors and bifunctors:

```haskell
type Maybe a = Either (Const () a) (Identity a)
```

These rules make derivation of functors a mechanical process that is automated in Haskell using the extension `{-# LANGUAGE DeriveFunctor #-}`.


Challenges
----------

> Solved with ideas taken from https://efanzh.org/2018/09/10/category-theory-for-programmers-challenges.html#functoriality

#### 1. Show that the data type:
```haskell
data Pair a b = Pair a b
```
is a bifunctor. For additional credit implement all three methods of `Bifunctor` and use equational reasoning to show that these definitions are compatible with the default implementations whenever they can be applied.

```haskell
-- Implementation
instance Bifunctor Pair where
    bimap g h (Pair x y) = Pair (g x) (h y)
    first g (Pair x y) = Pair (g x) y
    second h (Pair x y) = Pair x (h y)

-- bimap is compatible with the default implementation:
bimap g h (Pair x y)
    -- By the standard definition of bimap
    = (first g . second h) (Pair x y)
    -- By the definition of composition
    = first g (second h (Pair x y))
    -- By the implementation of second
    = first g (Pair x (h y))
    -- By the implementation of first
    = Pair (g x) (h y)

-- first is compatible with the default implementation:
first g (Pair x y)
    -- By the standard definition of first
    = bimap g id (Pair x y)
    -- By the implementation of bimap
    = Pair (g x) (id y)
    -- By the definition of identity
    = Pair (g x) y

-- second is compatible with the default implementation:
second h (Pair x y)
    -- By the standard definition of second
    = bimap id h (Pair x y)
    -- By the implementation of bimap
    = Pair (id x) (h y)
    -- By the definition of identity
    = Pair x (h y)
```


#### 2. Show the isomorphism between the standard definition of `Maybe` and this desugaring:
```haskell
type Maybe' a = Either ( Const () a) ( Identity a)
```
Hint: Define two mappings between the two implementations. For additional credit, show that they are the inverse of each other using equational reasoning.

See [Functoriality.hs](../haskell/Functoriality.hs)

```haskell
maybeToMaybe' :: Maybe a -> Maybe' a
maybeToMaybe' Nothing = Left (Const ())
maybeToMaybe' (Just x) = Right (Identity x)

maybe'ToMaybe :: Maybe' a -> Maybe a
maybe'ToMaybe (Left (Const ())) = Nothing
maybe'ToMaybe (Right (Identity x)) = Just x

-- Demonstrations:
maybe'ToMaybe (maybeToMaybe' (Nothing))
    = maybe'ToMaybe (Left (Const ()))
    = Nothing
maybe'ToMaybe (maybeToMaybe' (Just a))
    = maybe'ToMaybe (Right (Identity a))
    = Just a
```

#### 3. Let’s try another data structure. I call it a `PreList` because it’s a precursor to a `List`. It replaces recursion with a type parameter `b`.
```haskell
data PreList a b = Nil | Cons a b
```
You could recover our earlier definition of a `List` by recursively applying `PreList` to itself (we’ll see how it’s done when we talk about fixed points). Show that `PreList` is an instance of `Bifunctor`.

```haskell
-- Implementation
data PreList a b = Nil | Cons a b
instance Bifunctor PreList where
    bimap _ _ Nil = Nil
    bimap f g (Cons x y) = Cons (f x) (g y)

-- Composition law:
(bimap f g . bimap f' g') a = bimap (f . f') (g . g') a
-- Nil case
(bimap f g . bimap f' g') Nil
    -- By definition of composition
    = bimap f g (bimap f' g' Nil)
    -- By definition of bimap
    = bimap f g Nil
    -- By definition of bimap
    = Nil
    -- By definition of bimap
    = bimap (f . f') (g . g') Nil
-- Cons case
(bimap f g . bimap f' g') (Cons a b)
    -- By definition of composition
    = bimap f g (bimap f' g' (Cons a b))
    -- By definition of bimap
    = bimap f g (Cons (f' a) (g' b))
    -- By definition of bimap
    = Cons (f (f' a)) (g (g' b))
    -- By definition of composition
    = Cons ((f . f') a) ((g . g') b)
    -- By definition of bimap
    = bimap (f . f') (g . g') (Cons a b)

-- Identity law:
bimap id id a = a
-- Nil case
bimap id id Nil
    -- By definition of bimap
    = Nil
-- Cons case
bimap id id (Cons a b)
    -- By definition of bimap
    = Cons (id a) (id b)
    -- By definition of identity
    = Cons a b
```

#### 4. Show that the following data types define `bifunctors` in `a` and `b`:
```haskell
data K2 c a b = K2 c
data Fst a b = Fst a
data Snd a b = Snd b
```
For additional credit, check your solutions against Conor McBride’s paper [Clowns to the Left of me, Jokers to the Right 1](http://strictlypositive.org/CJ.pdf).

```haskell
-- Implementation
data K2 c a b = K2 c
instance Bifunctor (K2 c) where
    bimap _ _ (K2 x) = K2 x

-- Composition law:
(bimap f g . bimap f' g') a = bimap (f . f') (g . g') a
(bimap f g . bimap f' g') (K2 c)
    -- By definition of composition
    = bimap f g (bimap f' g' (K2 c))
    -- By definition of bimap
    = bimap f g (K2 c)
    -- By definition of bimap
    = K2 c
    -- By definition of bimap
    = bimap (f . f') (g . g') (K2 c)

-- Identity law:
bimap id id a = a
-- Nil case
bimap id id (K2 c)
    -- By definition of bimap
    = K2 c
```

```haskell
-- Implementation
data Fst a b = Fst a
instance Bifunctor Fst where
    bimap f _ (Fst x) = Fst (f x)

-- Composition law:
(bimap f g . bimap f' g') a = bimap (f . f') (g . g') a
(bimap f g . bimap f' g') (Fst a)
    -- By definition of composition
    = bimap f g (bimap f' g' (Fst a))
    -- By definition of bimap
    = bimap f g (Fst (f' a))
    -- By definition of bimap
    = Fst (f (f' a))
    -- By definition of composition
    = Fst ((f . f') a)
    -- By definition of bimap
    = bimap (f . f') (g . g') (Fst a)

-- Identity law:
bimap id id a = a
-- Nil case
bimap id id (Fst a)
    -- By definition of bimap
    = Fst (id a)
    -- By definition of identity
    = Fst a
```

```haskell
-- Implementation
data Snd a b = Snd b
instance Bifunctor Snd where
    bimap _ g (Snd y) = Snd (g y)

-- Composition law:
(bimap f g . bimap f' g') a = bimap (f . f') (g . g') a
(bimap f g . bimap f' g') (Snd b)
    -- By definition of composition
    = bimap f g (bimap f' g' (Snd b))
    -- By definition of bimap
    = bimap f g (Snd (g' b))
    -- By definition of bimap
    = Snd (g (g' b))
    -- By definition of composition
    = Snd ((g . g') b)
    -- By definition of bimap
    = bimap (f . f') (g . g') (Snd b)

-- Identity law:
bimap id id a = a
-- Nil case
bimap id id (Snd b)
    -- By definition of bimap
    = Snd (id b)
    -- By definition of identity
    = Snd b
```

#### 5. Define a `bifunctor` in a language other than Haskell. Implement `bimap` for a generic pair in that language.

See [Pair.java](../java/src/test/java/org/krloxz/training/cattheory/bifunctor/Pair.java)
